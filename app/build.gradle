plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id "kotlin-android-extensions"
    id 'kotlin-kapt'
    id 'com.akaita.android.easylauncher'
    id 'io.fabric'
    id 'androidx.navigation.safeargs.kotlin'
}

def keystorePropertiesFile = rootProject.file("secret.properties")
def keystoreProperties = new Properties()

android {
    compileSdkVersion 28
    signingConfigs {
        if (keystorePropertiesFile.exists()) {
            keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }
    defaultConfig {
        applicationId "at.florianschuster.watchables"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 25
        versionName "1.0.9"
        vectorDrawables.useSupportLibrary = true
        setProperty("archivesBaseName", "Watchables-v$versionName-b$versionCode")


        resValue "string", "app_name", "Watchables"
        buildConfigField "String", "MOVIEDB_BASE_URL", "\"https://api.themoviedb.org/3/\""
        buildConfigField "String", "MOVIEDB_POSTER_THUMBNAIL", "\"https://image.tmdb.org/t/p/w342\""
        buildConfigField "String", "MOVIEDB_POSTER_ORIGINAL", "\"https://image.tmdb.org/t/p/original\""


        if (keystorePropertiesFile.exists()) {
            buildConfigField "String", "MOVIEDB_KEY", keystoreProperties['tmdbKey']
        }
    }
    buildTypes {
        release {
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled = false
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/library-core_release.kotlin_module'
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation Dependencies.KotlinStd

    /**
     * Base
     */
    implementation Dependencies.Reaktor
    implementation Dependencies.Koordinator
    implementation Dependencies.AndroidUtil.values()

    /**
     * UI
     */
    implementation Dependencies.UI.values()
    implementation Dependencies.Navigation.values()
    implementation Dependencies.Lifecycle.Extensions
    kapt Dependencies.Lifecycle.Compiler

    /**
     * Dependency Injection
     */
    implementation Dependencies.Koin.values()

    /**
     * Networking
     */
    implementation Dependencies.Network.values()

    /**
     * Local
     */
    implementation Dependencies.Local.Hawk

    /**
     * Firebase
     */
    implementation Dependencies.Firebase.values()

    /**
     * Rx
     */
    implementation Dependencies.RxJava.values()
    implementation Dependencies.RxBinding.values()

    /**
     * Utils
     */
    implementation Dependencies.MiscUtil.values()
    debugImplementation Dependencies.LeakCanary.Op
    releaseImplementation Dependencies.LeakCanary.NoOp
    implementation Dependencies.Glide.Core
    implementation Dependencies.Glide.Transformation
    kapt Dependencies.Glide.Compiler
    implementation Dependencies.Work.values()
}


easylauncher {
    iconNames "@mipmap/ic_launcher_foreground"
    foregroundIconNames "@mipmap/ic_launcher_foreground"

    defaultFlavorNaming = true

    //For customization see: https://github.com/akaita/easylauncher-gradle-plugin#usage.
    buildTypes {
        debug {
            filters = customColorRibbonFilter("debug", "#ffffff", "#47BAF2")
        }
        release {}
    }
}

apply plugin: 'com.google.gms.google-services'
