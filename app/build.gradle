plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id "kotlin-android-extensions"
    id 'kotlin-kapt'
    id 'com.akaita.android.easylauncher'
    id 'io.fabric'
    id 'androidx.navigation.safeargs.kotlin'
}

def keystorePropertiesFile = rootProject.file("secret.properties")
def keystoreProperties = new Properties()

android {
    compileSdkVersion 29
    signingConfigs {
        if (keystorePropertiesFile.exists()) {
            keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }
    defaultConfig {
        applicationId "at.florianschuster.watchables"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 37
        versionName "1.0.11"
        vectorDrawables.useSupportLibrary = true
        setProperty("archivesBaseName", "Watchables-v$versionName-b$versionCode")


        resValue "string", "app_name", "Watchables"
        buildConfigField "String", "MOVIEDB_BASE_URL", "\"https://api.themoviedb.org/3/\""
        buildConfigField "String", "MOVIEDB_POSTER_THUMBNAIL", "\"https://image.tmdb.org/t/p/w342\""
        buildConfigField "String", "MOVIEDB_POSTER_ORIGINAL", "\"https://image.tmdb.org/t/p/original\""


        if (keystorePropertiesFile.exists()) {
            buildConfigField "String", "MOVIEDB_KEY", keystoreProperties['tmdbKey']
        }
    }
    buildTypes {
        release {
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled = false
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    packagingOptions {
        exclude 'META-INF/library-core_release.kotlin_module'
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation Dependencies.KotlinStd

    /**
     * Base
     */
    implementation Dependencies.Reaktor
    implementation Dependencies.Koordinator

    implementation Dependencies.AndroidUtilUI
    implementation Dependencies.AndroidUtilAsync
    implementation Dependencies.AndroidUtilNetwork
    implementation Dependencies.AndroidUtilOptional
    implementation Dependencies.AndroidUtilPermissions
    implementation Dependencies.AndroidUtilValidation
    implementation Dependencies.AndroidUtilViewState
    implementation Dependencies.AndroidUtilFirebase


    /**
     * UI
     */
    implementation Dependencies.AppCompat
    implementation Dependencies.Material
    implementation Dependencies.Fragment
    implementation Dependencies.Constraintlayout
    implementation Dependencies.CoreKTX

    implementation Dependencies.NavigationUI
    implementation Dependencies.NavigationFragment
    implementation Dependencies.LifecycleExtensions
    kapt Dependencies.LifecycleCompiler

    /**
     * Dependency Injection
     */
    implementation Dependencies.KoinCore
    implementation Dependencies.KoinAndroid
    implementation Dependencies.KoinAndroidScope
    implementation Dependencies.KoinAndroidViewModel

    /**
     * Networking
     */
    implementation Dependencies.Retrofit
    implementation Dependencies.RetrofitGsonConverter
    implementation Dependencies.RetrofitRxJava2Adapter
    implementation Dependencies.OkHttp
    implementation Dependencies.OkHttpLoggingInterceptor

    /**
     * Local
     */
    implementation Dependencies.Hawk

    /**
     * QR
     */
    implementation Dependencies.ZxingCore
    implementation Dependencies.ZxingAndroid

    /**
     * Firebase
     */
    implementation Dependencies.FirebaseCore
    implementation Dependencies.FirebaseAuth
    implementation Dependencies.PlayServicesAuth
    implementation Dependencies.FirebaseFirestore
    implementation Dependencies.FirebaseCrashlytics
    implementation Dependencies.FirebaseMessaging
    implementation Dependencies.FirebasePrformance
    implementation Dependencies.FirebaseInvites

    /**
     * Rx
     */
    implementation Dependencies.RxJava2
    implementation Dependencies.RxJava2Kotlin
    implementation Dependencies.RxJava2Relay
    implementation Dependencies.RxJava2Android

    implementation Dependencies.RxBinding
    implementation Dependencies.RxBindingCore
    implementation Dependencies.RxBindingAppcompat
    implementation Dependencies.RxBindingRecyclerview
    implementation Dependencies.RxBindingMaterial

    /**
     * Utils
     */
    implementation Dependencies.Gson
    implementation Dependencies.Timber
    implementation Dependencies.Threeten
    implementation Dependencies.Flick
    implementation Dependencies.Gestureviews
    implementation Dependencies.Aboutlibs
    implementation Dependencies.Customtabs
    implementation Dependencies.RxPermissions
    implementation Dependencies.Shimmer

    debugImplementation Dependencies.LeakCanary
    releaseImplementation Dependencies.LeakCanaryNoOp

    implementation Dependencies.Glide
    kapt Dependencies.GlideCompiler
    implementation Dependencies.GlideTransformation

    implementation Dependencies.Worker
    implementation Dependencies.WorkerRxJava2

    testImplementation Dependencies.Test.values()
    androidTestImplementation Dependencies.AndroidTest.values()
}


easylauncher {
    iconNames "@mipmap/ic_launcher_foreground"
    foregroundIconNames "@mipmap/ic_launcher_foreground"

    defaultFlavorNaming = true

    //For customization see: https://github.com/akaita/easylauncher-gradle-plugin#usage.
    buildTypes {
        debug {
            filters = customColorRibbonFilter("debug", "#ffffff", "#47BAF2")
        }
        release {}
    }
}

apply plugin: 'com.google.gms.google-services'
